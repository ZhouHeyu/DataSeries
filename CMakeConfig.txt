#
# (c) Copyright 2008, Hewlett-Packard Development Company, LP
#
#  See the file named COPYING for license details
#
# All of the options and dependencies for the various cmake sub-bits

#### Shared Libs

OPTION(BUILD_SHARED_LIBS "Should we build shared libraries" ON)

IF(BUILD_SHARED_LIBS)
    SET(LIBRARY_TYPE SHARED)
ELSE(BUILD_SHARED_LIBS)
    SET(LIBRARY_TYPE STATIC)
ENDIF(BUILD_SHARED_LIBS)

#### Lintel

SET(LINTEL_FIND_REQUIRED ON)
SET(LINTELPTHREAD_FIND_REQUIRED ON)
INCLUDE(FindLintel)

#### LibXML2

INCLUDE(FindLibXml2)
IF(NOT LIBXML2_FOUND)
    MESSAGE(FATAL_ERROR "DataSeries requires libxml2 to build")
ENDIF(NOT LIBXML2_FOUND)

#### Zlib

INCLUDE(FindZLIB)

OPTION(WITH_ZLIB "Build with zlib (gzip) support?" ON)

IF(WITH_ZLIB AND ZLIB_FOUND) 
    SET(ZLIB_ENABLED ON)
ELSE(WITH_ZLIB AND ZLIB_FOUND) 
    SET(ZLIB_ENABLED OFF)	    
ENDIF(WITH_ZLIB AND ZLIB_FOUND) 

IF(WITH_ZLIB AND NOT ZLIB_ENABLED)
    MESSAGE("WITH_ZLIB on, but could not find zlib includes/libraries.")
    MESSAGE("  zlib (gzip) compression support will be skipped.")
ENDIF(WITH_ZLIB AND NOT ZLIB_ENABLED)

#### BZip2

INCLUDE(FindBZip2)

OPTION(WITH_BZIP2 "Build with bzip2 support?" ON)

IF(WITH_BZIP2 AND BZIP2_FOUND) 
    SET(BZIP2_ENABLED ON)
ELSE(WITH_BZIP2 AND BZIP2_FOUND) 
    SET(BZIP2_ENABLED OFF)	    
ENDIF(WITH_BZIP2 AND BZIP2_FOUND) 

IF(WITH_BZIP2 AND NOT BZIP2_ENABLED)
    MESSAGE("WITH_BZIP2 on, but could not find bzip2 includes/libraries.")
    MESSAGE("  bzip2 compression support will be skipped.")
ENDIF(WITH_BZIP2 AND NOT BZIP2_ENABLED)

SET(BUNZIP2_PROGRAM_MISSING_EXTRA 
    "  will not be able to run the pssimple regression test")
LINTEL_WITH_PROGRAM(BUNZIP2_PROGRAM bunzip2)

#### SRT

SET(SRT_MISSING_EXTRA "  will skip building srt2ds, cmpsrtds")
LINTEL_WITH_LIBRARY(SRT SRT/SRTTrace.H SRTlite)
IF(SRT_ENABLED) # hack
    SET(FULL_SRT_INCLUDE_DIR "${SRT_INCLUDE_DIR}/SRT")
ENDIF(SRT_ENABLED)

#### Extra libraries

SET(LZO_MISSING_EXTRA "  lzo compression support will be skipped.")
INCLUDE(FindLZO)

#### crypto

SET(CRYPTO_MISSING_EXTRA "  will skip building iphost2ds, nettrace2ds")
INCLUDE(FindCrypto)

SET(PCRE_MISSING_EXTRA "  will skip building bacct2ds")
LINTEL_WITH_LIBRARY(PCRE pcre.h pcre)

SET(PCAP_MISSING_EXTRA "  will skip building nettrace2ds, network-driverdump")
LINTEL_WITH_HEADER(PCAP pcap.h)

SET(LINUX_COMPILER_MISSING_EXTRA "  will skip building lindump-mmap")
LINTEL_WITH_HEADER(LINUX_COMPILER linux/compiler.h)

#### Perl

LINTEL_FIND_PERL_MODULE(XML::Parser PERL_XML_PARSER)
LINTEL_FIND_PERL_MODULE(Date::Parse PERL_DATE_PARSE)
LINTEL_FIND_PERL_MODULE(Crypt::Rijndael PERL_CRYPT_RIJNDAEL)

#### Doxygen

INCLUDE(LintelDocs)

LINTEL_DOCS_CONFIG("DataSeries")

#### Misc Options

OPTION(BUILD_PAPER "Build the src/paper programs" OFF)
OPTION(BUILD_EXAMPLE "Build the example program" OFF)

#### RPath

OPTION(WITH_INSTALLED_RPATH "Install with the rpath set so you will not need to set \$LD_LIBRARY_PATH" ON)

IF(WITH_INSTALLED_RPATH)
  # use, i.e. don't skip the full RPATH for the build tree
  SET(CMAKE_SKIP_BUILD_RPATH  FALSE)

  # when building, don't use the install RPATH already
  # (but later on when installing)
  SET(CMAKE_BUILD_WITH_INSTALL_RPATH FALSE) 

  # the RPATH to be used when installing
  SET(CMAKE_INSTALL_RPATH "${CMAKE_INSTALL_PREFIX}/lib")

  # add the automatically determined parts of the RPATH
  # which point to directories outside the build tree to the install RPATH
  SET(CMAKE_INSTALL_RPATH_USE_LINK_PATH TRUE)
ENDIF(WITH_INSTALLED_RPATH)
